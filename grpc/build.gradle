plugins {
  id "com.google.protobuf" version "0.8.17"
  id "application"
}
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.44.1'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }

    generatedFilesBaseDir = "$buildDir/generated/protobuf/"
}

sourceSets {
    main {
        java {
            srcDirs "$protobuf.generatedFilesBaseDir/main/grpc"
            srcDirs "$protobuf.generatedFilesBaseDir/main/java"
        }
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'io.grpc:grpc-all:1.5.0'
}


task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'se.anders_raberg.grpc.HelloWorldServer'
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'se.anders_raberg.grpc.HelloWorldClient'
}

startScripts.enabled = false

task helloWorldServer(type: CreateStartScripts) {
    mainClassName = "se.anders_raberg.grpc.HelloWorldServer"
    applicationName = "hello-world-server"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtimeOnly
}

task helloWorldClient(type: CreateStartScripts) {
    mainClassName = "se.anders_raberg.grpc.HelloWorldClient"
    applicationName = "hello-world-client"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtimeOnly
}

applicationDistribution.into("bin") {
    from(helloWorldServer)
    from(helloWorldClient)
    fileMode = 0755
}
